Proceso Practica31
	Definir num, contadorPares, contadorImpares, anterior Como Entero;
	Definir paso_cero, avisoParImpar Como Logico;
	// Inicializar variables auxiliares
	contadorImpares <- 0;
	contadorPares <- 0;
	paso_cero <- Falso; // Inicialmente asumimos que no paso el cero
	avisoParImpar <- Verdadero; // Asumimos que la secuencia es par-impar-par-impar
	anterior <- 0;
	// Entrada
	Escribir "Ingrese un numero entero, finaliza con negativo";
	Leer num;
	//Proceso: ciclo y validaciones
	Mientras num >= 0 Hacer
		Si num <> 0 Entonces
			Si num mod 2 == 0 Entonces
				contadorPares <- contadorPares + 1;
				Si anterior mod 2 == 0 y anterior <> 0 Entonces
					avisoParImpar <- Falso;
				FinSi
			SiNo
				contadorImpares <- contadorImpares + 1;
				Si anterior mod 2 <> 0 y anterior <> 0 Entonces
					avisoParImpar <- Falso;
				FinSi
			FinSi
		SiNo
			paso_cero <- Verdadero;
			avisoParImpar <- Falso;
		FinSi
		anterior = num; // Para no perder el valor de la lectura anterior
		Escribir "Ingrese un numero entero, finaliza con negativo";
		Leer num;
	FinMientras
	Escribir "Cantidad de pares: ", contadorPares, " y de impares: ", contadorImpares;
	si paso_cero == Verdadero Entonces
		Escribir "Paso al menos un cero";
	FinSi
	si avisoParImpar == Verdadero Entonces
		Escribir "Paso secuencia par-impar";
	FinSi
FinProceso
